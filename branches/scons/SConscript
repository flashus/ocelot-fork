#! /bin/python

################################################################################
#	\file   SConscript
#	\author Gregory Diamos <gregory.diamos@gatech.edu>
#   \date   Tuesday March 8, 2011
#	\brief  The SCons master build script for Ocelot
################################################################################

import os
import inspect

## Hepler functions
def config_h_build(target, source, env):

	config_h_defines = env.Dictionary()

	for a_target, a_source in zip(target, source):
		config_h = file(str(a_target), "w")
		config_h_in = file(str(a_source), "r")
		config_h.write(config_h_in.read() % config_h_defines)
		config_h_in.close()
		config_h.close()


## The script begins here
# try to import an environment first
try:
	Import('env')
except:
	try:
		exec open("../build/which.py")
		prefix = '../'
	except:
		exec open("build/which.py")
		prefix = './'
	exec open(prefix + "build/build_environment.py")
	env = Environment()
  
# on mac we have to tell the linker to link against the C++ library
if env['PLATFORM'] == "darwin":
	env.Append(LINKFLAGS = "-lstdc++")

# Create configure.h
env.AlwaysBuild(env.Command('configure.h', 'configure.h.in', config_h_build))

# find all source files in the source tree
sources = []
directories = ['ocelot/ir/implementation', 
	'ocelot/analysis/implementation',
	'ocelot/api/implementation', 
	'ocelot/cal/implementation', 
	'ocelot/cuda/implementation', 
	'ocelot/executive/implementation', 
	'ocelot/parser/implementation', 
	'ocelot/trace/implementation', 
	'ocelot/translator/implementation', 
	'ocelot/util/implementation', 
	'hydrazine/implementation', 
	'hydrazine/interface' ]
extensions = ['*.cpp']

for dir in directories:
	for ext in extensions:
		regexp = os.path.join(dir, ext)
		sources.extend(env.Glob(regexp))

# find all flex/bison sources
flex_extensions  = ['*.ll']
bison_extensions = ['*.yy']

flex_sources  = []
bison_sources = []

for dir in directories:
	for ext in flex_extensions:
		regexp = os.path.join(dir, ext)
		flex_sources.extend(env.Glob(regexp))
	for ext in bison_extensions:
		regexp = os.path.join(dir, ext)
		bison_sources.extend(env.Glob(regexp))
	
# Generate the flex/bison .c/.cpp files
for source in flex_sources:
	lex = env.CXXFile(os.path.splitext(os.path.basename(str(source)))[0] \
		+ '.cpp', str(source))
	sources.append(lex[0])

for source in bison_sources:
	bison = env.CXXFile(os.path.splitext(os.path.basename(str(source)))[0] \
		+ '.cpp', str(source))
	sources.append(bison[0])

# Create the ocelot library
libocelot = env.SharedLibrary('ocelot', sources)

# Create the ocelot tools
ocelot_libs = env['EXTRA_LIBS']
ocelot_libs.extend(env['LLVM_LIBS'])

OcelotConfig = env.Program('OcelotConfig', \
	[libocelot, 'ocelot/tools/OcelotConfig.cpp'], LIBS=ocelot_libs)
PTXOptimizer = env.Program('PTXOptimizer', \
	[libocelot, 'ocelot/tools/PTXOptimizer.cpp'], LIBS=ocelot_libs)
OcelotServer = env.Program('OcelotServer', \
	[libocelot, 'ocelot/tools/OcelotServer.cpp'], LIBS=ocelot_libs)
OcelotServer = env.Program('OcelotKernelTestHarness', \
	[libocelot, 'ocelot/tools/KernelTestHarness.cpp'], LIBS=ocelot_libs)

Default(OcelotConfig)

# Create the ocelot unit tests
tests = []
tests.append(('TestLexer',  'ocelot/parser/test/TestLexer.cpp', 'basic'))
tests.append(('TestParser', 'ocelot/parser/test/TestParser.cpp', 'basic'))
tests.append(('TestInstructions', \
	'ocelot/executive/test/TestInstructions.cpp', 'basic'))
tests.append(('TestDataflowGraph', \
	'ocelot/analysis/test/TestDataflowGraph.cpp', 'basic'))
tests.append(('TestLLVMInstructions', \
	'ocelot/ir/test/TestLLVMInstructions.cpp', 'basic'))
tests.append(('TestKernels', \
	'ocelot/executive/test/TestKernels.cpp', 'basic'))
tests.append(('TestLLVMKernels', \
	'ocelot/executive/test/TestLLVMKernels.cpp', 'basic'))
tests.append(('TestEmulator', \
	'ocelot/executive/test/TestEmulator.cpp', 'basic'))
tests.append(('TestPTXToLLVMTranslator', \
	'ocelot/translator/test/TestPTXToLLVMTranslator.cpp', 'basic'))
tests.append(('TestCudaSequence', \
	'ocelot/cuda/test/kernels/sequence.cu.cpp', 'full'))
tests.append(('TestCudaGenericMemory', \
	'ocelot/cuda/test/memory/generic.cpp', 'full'))
tests.append(('TestCudaMalloc', \
	'ocelot/cuda/test/memory/malloc.cu.cpp', 'full'))
tests.append(('TestCudaGlobals', \
	'ocelot/cuda/test/globals/global.cu.cpp', 'full'))
tests.append(('TestCudaTexture2D', \
	'ocelot/cuda/test/textures/texture2D.cu.cpp', 'full'))
tests.append(('TestCudaTexture3D', \
	'ocelot/cuda/test/textures/texture3D.cu.cpp', 'full'))
tests.append(('TestCudaTextureArray', \
	'ocelot/cuda/test/textures/textureArray.cu.cpp', 'full'))
tests.append(('TestFunctionCall', \
	'ocelot/cuda/test/functions/simpleFunc.cu.cpp', 'full'))
tests.append(('TestIndirectFunctionCall', \
	'ocelot/cuda/test/functions/indirectCall.cu.cpp', 'full'))
tests.append(('TestIndirectFunctionCallOcelot', \
	'ocelot/cuda/test/functions/indirectCallOcelot.cpp', 'full'))
tests.append(('TestCalVectorScale', \
	'ocelot/cal/test/vectorScale.cu.cpp', 'full'))
tests.append(('TestDeviceSwitching', \
	'ocelot/api/test/TestDeviceSwitching.cpp', 'full'))
tests.append(('TestPTXAssembly', \
	'ocelot/ir/test/TestPTXAssembly.cpp', 'full'))

for test in tests:
	Test = env.Program(test[0], [libocelot, test[1]], LIBS=ocelot_libs)

if env['test_level'] != 'none':
	print 'Adding unit tests to the build...'

level_map = { 'none' : 0, 'basic' : 1, 'full' : 2 }

for test in tests:
	if level_map[env['test_level']] >= level_map[test[2]]:
		print 'Adding test ' + test[0]
		Default(test[0])
